grammar de.fhws.rdsl.RDSL with org.eclipse.xtext.common.Terminals

generate rdsl "http://www.fhws.de/rdsl"
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

Model:
    configuration=Configuration | package=Package;

Package:
    (elements+=PackageElement)*;

PackageElement:
    Type;

Type:
    ResourceType;

ResourceType:
    RootResourceType | SubResourceType | ReferenceSchema;



SubResourceType:
    'SubResourceType' name=ID
    '{'
    (members+=Member)+
    '}';

RootResourceType:
    'RootResourceType' name=ID
    '{'
    (members+=Member)+
    '}';

ReferenceSchema: // Attribute an Referenzen! Wird nicht verwendet!
    'ReferenceSchema' name=ID
    '{'
    (attributes+=Attribute)+
    '}'
;

Member:
    Attribute | Reference | Containment;

Containment:
	(method=HTTPMETHOD)? name=ID ':' 'contains'
    (list?='many of')?
    resourceType=[SubResourceType]
    ;

Reference:
    (method=HTTPMETHOD)? name=ID ':'    
    'references'
    (list?='many of')?    
    resourceType=[RootResourceType]
    '->' opposite=ID
    ('as' referenceSchema=[ReferenceSchema])?
    ;

Attribute:
    (method=HTTPMETHOD)? name=ID ':'
    (list?='many of')?
    primitiveType=PrimitiveType
    (queryable?='queryable')?
    ;    

PrimitiveType :
	IntType | StringType | DateType | FloatType | BooleanType
;

BooleanType:
	text='Boolean'
;

FloatType:
	text='Float' ('(' (start=FLOAT)? '..' (stop=FLOAT)? ')')?
;

DateType:
	text='Date'
;

StringType:
	text='String'
;

IntType:
	text='Int' ('(' (start=INT)? '..' (stop=INT)? ')')?
;

Configuration:
    'Config'
    '{'
    'Base Package' package=FQN
    'REST Configuration' restConfiguration=RESTConfiguration
    'Database Configuration' databaseConfiguration = DatabaseConfiguration
    '}';

DatabaseConfiguration:
	RiakConfiguration | MySQLConfiguration
;

MySQLConfiguration:
	'MySQL {'
	'Test Connection File' testConnectionFile=STRING
	'}'
;

RiakConfiguration:
	'Riak'
	'{'
	'String Analyzer' name=('standard_analyzer_factory' | 'whitespace_analyzer_factory')
	'Test Connection File' testConnectionFile=STRING
	'}'
;

RESTConfiguration: 
	'{'
	'MIME' (mimeType+=MIME)+
    ('Framework' framework=FRAMEWORK)?
    'ID-Generation' idtype=ID_GEN?
    ('Authentication' auth=Auth)?
    ('API-Version' 'v=' apiVersion=VERSION)?
    ('Headers' headers+=Header+)?
    'Caching' caching=Caching
    'Paging' paging=Paging
	'}'
;

Caching:
    type=CACHING_TYPE ('seconds' time=TIME)?;

Paging:
    'size' elementsCount=INT;

Start:
    startPath=URL resource=[ResourceType];

Header:
    name=HEADER_STRING ('Description' description=STRING)? 'default value=' value=STRING;

Auth:
    HTTPBasic | OAuth2;

OAuth2:
    'OAuth2'
    '{'
    'URI' authPath=STRING
    'Encoding' encodign=ENCODING_TYPE
    'Scope' scoep=STRING
    '}';

HTTPBasic:
    'HTTPBasic'
    '{'
    'Username' user=STRING
    'Password' password=STRING
    'Role' role=ROLE
    '}';

FQN:
    ID ("." ID)*;

enum CACHING_TYPE:
    ETAG='ETag' | MODIFIED='Last-Modified' | EXPIRES='Expires' | MAXAGE='Max-Age' | DEFAULT='Default' | NONE='None';

enum MIME:
    JSON='json' | XML='xml';

enum ID_GEN:
    LONG='long' | UUID='uuid';

enum HTTPMETHOD:
    GET | PUT | POST | PATCH | DELETE | HEAD | OPTIONS | DEFAULT | NONE;

enum FRAMEWORK:
    SPRING='Spring' | JERSEY='Jersey';

enum ROLE:
    ADMIN='admin' | USER='user';

enum ENCODING_TYPE:
    BASE64='base64' | NONE='none';

terminal URL:
    'http' 's'? '://' ((ID '.' ID) ('.' ID)* | "localhost" | (INT '.' INT '.' INT '.' INT)) ':' INT+ ('/' ID+)*;

terminal HEADER_STRING:
    'x-' ID ('-' ID)*;

terminal VERSION:
    ('1'..'9');

terminal TIME:
    ('1'..'9')+;
    
terminal FLOAT: (INT+'.'INT+);
    
